plugins {
    id "java"
    id "application"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "ru.bukhtaev"
version = "1.0-SNAPSHOT"
mainClassName = "ru.bukhtaev.Main"

def encoding = "UTF-8"
def jarName = "weather-app-fat-jar"

ext {
    lombokVersion = "1.18.30"
    jUnitVersion = "5.9.1"
    mockitoVersion = "5.5.0"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = encoding
}
tasks.withType(Javadoc).configureEach {
    options.encoding = encoding
}

// Таска с использованием специального плагина Shadow.
shadowJar {
    archiveBaseName.set(jarName + "-shadow")

    configurations = [project.configurations.runtimeClasspath]
}

// Таска без использования специального плагина.
tasks.register("fatJar", Jar) {
    manifest {
        attributes "Main-Class": mainClassName
    }

    archiveBaseName.set(jarName + "-native")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    testImplementation platform("org.junit:junit-bom:$jUnitVersion")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

test {
    useJUnitPlatform()
}